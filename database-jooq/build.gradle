apply plugin: 'java'
apply plugin: 'io.ehdev.gradle.development'

sourceSets {
    main {
        java {
            srcDir 'src/main/generated-java'
        }
    }
}

configurations {
    jooq
}

dependencies {
    jooq deps.libraries.jooqCodeGen

    compile deps.libraries.database
    compile deps.libraries.validations
}

task prepairJooqConfig(type: Copy) {
    from "$projectDir/src/config/library.xml"
    into "$buildDir/jooq"
    outputs.upToDateWhen { false }
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'JDBC_URL' : database.url.replace("&", '&amp;'),
            'DB_USER'  : database.user,
            'DB_PASSWD': database.password,
            'SCHEMA'   : database.name,
            'DEST'     : "$projectDir/src/main/generated-java" as String
    ])
}

task generateClassesFromDatabase(type: JavaExec) {
    dependsOn ':database-migration:flywayMigrate'
    dependsOn prepairJooqConfig
    classpath configurations.jooq
    main =  'org.jooq.codegen.GenerationTool'
    args "$buildDir/jooq/library.xml"
}