plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id 'com.google.cloud.tools.jib' version '0.10.0'
}

dependencies {
    compile deps.libraries.kotlin
    compile deps.libraries.jackson

    compile deps.libraries.logging

    compile deps.libraries.spring

    compile deps.libraries.common

    compile deps.libraries.database

    compile deps.libraries.oauth

    compile deps.libraries.freemarker

    annotationProcessor deps.libraries.springAnnotation

    compile deps.libraries.jwt

    compile deps.libraries.commonsLang

    compile project(":models")
    compile project(":database-manager")
    compile project(":database-migration")
    
    testCompile deps.libraries.testingLibraries
    testCompile deps.libraries.springTest
}

configurations.compile {
    exclude group: 'ch.qos.logback'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
}

mainClassName = 'io.ehdev.account.web.AccountApplication'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jib {
    to {
        image = 'gcr.io/account-manager-216313/account-manager-app'
        tags = [version.toString(), 'latest']
        auth {
            username = '_json_key'
        }
    }
}

task copyDistForRelease(type: Copy, dependsOn: 'distZip') {
    from distZip.archivePath
    into "${rootDir}/build/release-dir"
    rename distZip.archiveName, 'account-manager.zip'
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}